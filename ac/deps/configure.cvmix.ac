# Autoconf configuration
AC_INIT(
    [CVMix],
    [ ],
    [https://github.com/CVMix/CVMix-src])

# Validate srdcir and configure input
AC_CONFIG_SRCDIR([cvmix_utils.F90])
AC_CONFIG_MACRO_DIR([m4])


# Standard Fortran configuration
AC_LANG(Fortran)
AC_FC_SRCEXT(f90)
AC_PROG_FC


# Force 8-byte reals
AX_FC_REAL8
AS_IF(
  [test "$enable_real8" != no],
  [FCFLAGS="$FCFLAGS $REAL8_FCFLAGS"])


# Unlimited line length
#AC_FC_LINE_LENGTH([unlimited])

## Allow invaliz BOZ assignment
#AX_FC_ALLOW_INVALID_BOZ
#FCFLAGS="$FCFLAGS $ALLOW_INVALID_BOZ_FCFLAGS"


## Allow argument mismatch (for functions lacking interfaces)
#AX_FC_ALLOW_ARG_MISMATCH
#FCFLAGS="$FCFLAGS $ALLOW_ARG_MISMATCH_FCFLAGS"


# Search for mkmf build tools
AC_PATH_PROG([LIST_PATHS], [list_paths])
AS_IF([test -z "$LIST_PATHS"], [
  AC_PATH_PROG([LIST_PATHS], [list_paths], [], ["$PATH:${srcdir}/../../../bin"])
  AS_IF([test -z "$LIST_PATHS"],
    [AC_MSG_ERROR([Could not find list_paths.])],
    [AC_SUBST(PATH, ["$PATH:${srcdir}/../../../bin"])])
  ]
)

AC_PATH_PROG([MKMF], [mkmf])
AS_IF([test -z "$MKMF"], [
  AC_PATH_PROG([MKMF], [mkmf], [], ["$PATH:${srcdir}/../../../bin"])
  AS_IF([test -z "$MKMF"],
    [AC_MSG_ERROR([Could not find mkmf.])],
    [AC_SUBST(PATH, ["$PATH:${srcdir}/../../../bin"])])
  ]
)


# MKMF commands
AC_CONFIG_COMMANDS([path_names],
  [${LIST_PATHS} -l ${srcdir}],
  [LIST_PATHS=${LIST_PATHS}])


AC_CONFIG_COMMANDS([mkmf],
  [${MKMF} -p libcvmix.a -m Makefile.mkmf path_names],
  [MKMF=${MKMF}])


# Autoconf does not configure the archiver (ar), as it is handled by Automake.
# TODO: Properly configure this tool.  For now, we hard-set this to `ar`.
AR=ar
ARFLAGS=rv
AC_SUBST(AR)
AC_SUBST(ARFLAGS)


# Prepare output
AC_SUBST(CPPFLAGS)
AC_CONFIG_FILES(Makefile)
AC_OUTPUT
